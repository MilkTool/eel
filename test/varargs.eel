/////////////////////////////////////////////
// Temporary EEL Test Suite
// Copyright 2002-2005, 2014 David Olofson
/////////////////////////////////////////////

procedure verify(f, result, correct)
{
	print("  ", f.name, " returned \"", result,"\" ; should be \"", correct);
	if result == correct
		print("\" PASS\n");
	else
	{
		print("\" FAIL\n");
		throw "Incorrect result!";
	}
}

procedure try_call(f, mask)
{
	local m = 0;
	try
	{
		verify(f, f(), "");
		m = m | 0b1;
	}
	except
		print("  ", f.name, " won't accept 0 arguments.\n");
	try
	{
		verify(f, f("a"), "a");
		m = m | 0b10;
	}
	except
		print("  ", f.name, " won't accept 1 argument.\n");
	try
	{
		verify(f, f("a", "b"), "ab");
		m = m | 0b100;
	}
	except
		print("  ", f.name, " won't accept 2 arguments.\n");
	try
	{
		verify(f, f("a", "b", "c"), "abc");
		m = m | 0b1000;
	}
	except
		print("  ", f.name, " won't accept 3 arguments.\n");
	try
	{
		verify(f, f("a", "b", "c", "A"), "abcA");
		m = m | 0b10000;
	}
	except
		print("  ", f.name, " won't accept 4 arguments.\n");
	try
	{
		verify(f, f("a", "b", "c", "A", "B"), "abcAB");
		m = m | 0b100000;
	}
	except
		print("  ", f.name, " won't accept 5 arguments.\n");
	try
	{
		verify(f, f("a", "b", "c", "A", "B", "C"), "abcABC");
		m = m | 0b1000000;
	}
	except
		print("  ", f.name, " won't accept 6 arguments.\n");
	if m == mask
		print("    PASS\n");
	else
	{
		print("    FAIL\n");
		throw "Incorrect result!";
	}
}

function req(a, b, c)
{
	return a + b + c;
}

function opt[a, b, c]
{
	local r = "";
	if specified a
		r = r + a;
	if specified b
		r = r + b;
	if specified c
		r = r + c;
	return r;
}

function tup<a, b, c>
{
	local r = "";
	for local t = 0, tuples - 1
		r = r + a[t] + b[t] + c[t];
	return r;
}

function req_opt(a)[b, c]
{
	local r = a;
	if specified b
		r = r + b;
	if specified c
		r = r + c;
	return r;
}

function req_tup(a)<b, c>
{
	local r = a;
	for local t = 0, tuples - 1
		r = r + b[t] + c[t];
	return r;
}

function req_opt_tup(a)[b]<c, d>
{
	local r = a;
	if specified b
		r = r + b;
	for local t = 0, tuples - 1
		r = r + c[t] + d[t];
	return r;
}

export function main<args>
{
	print("Varargs tests:\n");

	print("\nEarly binding (compile time checking):\n");
	print("------------------------------------------\n");
	verify(req, req("a", "b", "c"), "abc");
	print("------------------------------------------\n");
	verify(opt, opt(), "");
	verify(opt, opt("a"), "a");
	verify(opt, opt("a", "b"), "ab");
	verify(opt, opt("a", "b", "c"), "abc");
	print("------------------------------------------\n");
	verify(tup, tup(), "");
	verify(tup, tup("a", "b", "c"), "abc");
	verify(tup, tup("a", "b", "c", "A", "B", "C"), "abcABC");
	print("------------------------------------------\n");
	verify(req_opt, req_opt("a"), "a");
	verify(req_opt, req_opt("a", "b"), "ab");
	verify(req_opt, req_opt("a", "b", "c"), "abc");
	print("------------------------------------------\n");
	verify(req_tup, req_tup("a"), "a");
	verify(req_tup, req_tup("a", "b", "c"), "abc");
	verify(req_tup, req_tup("a", "b", "c", "B", "C"), "abcBC");
	print("------------------------------------------\n");
	verify(req_opt_tup, req_opt_tup("a"), "a");
	verify(req_opt_tup, req_opt_tup("a", "b"), "ab");
	verify(req_opt_tup, req_opt_tup("a", "b", "c", "d"), "abcd");
	verify(req_opt_tup, req_opt_tup("a", "b", "c", "d", "C", "D"),
			"abcdCD");
	print("------------------------------------------\n");

	print("\nLate binding (run time checking):\n");
	print("------------------------------------------\n");
	try_call(req, 0b0001000);
	print("------------------------------------------\n");
	try_call(opt, 0b0001111);
	print("------------------------------------------\n");
	try_call(tup, 0b1001001);
	print("------------------------------------------\n");
	try_call(req_opt, 0b0001110);
	print("------------------------------------------\n");
	try_call(req_tup, 0b0101010);
	print("------------------------------------------\n");
	try_call(req_opt_tup, 0b1010110);
	print("------------------------------------------\n");

	print("Varargs tests done.\n");
	return 0;
}
