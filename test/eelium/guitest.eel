//////////////////////////////////////////////////////////////
// guitest.eel - Test of eelium.gui
// Copyright 2005, 2007, 2009, 2011, 2014, 2020 David Olofson
//////////////////////////////////////////////////////////////

import math, SDL as SDL, eelium.gui as gui;

static do_exit = false;
static askexit_up = false;
static bcnt, static tcnt = 0;
static work, static container;
static ls = [];


procedure ask_exit
{
	if askexit_up
		return;
	local r = SDL.Rect [0, 0, 200, 100];
	r.(x, y) = (gui.GetTarget().(w, h) - r.(w,h)) / 2;
	local win = gui.Window(r);
	win:SetAlign("grid", 1, 2, nil, vector [3, 1]);
	gui.Label(win, "Really quit?");
	local p = gui.Panel(win, "none");
	p:SetAlign("grid", 2, 1);
	gui.Button(p, "Yes", procedure(self) { do_exit = true; });
	local b = gui.Button(p, "No", procedure(self)
			{
				self.win:Delete();
				askexit_up = false;
			});
	b.win = win;
	gui.SetModal(win);
	gui.SetFocus(b);
	askexit_up = true;
}


procedure spawn(what)
{
	switch what % 11
	  case 0
	  {
		gui.Button(container, "Button " +
				(string)(bcnt + 1),
				procedure(self)
				{
					print(self.caption,
							" clicked!\n");
				});
		bcnt += 1;
	  }
	  case 1
	  {
		gui.Toggle(container, "Toggle " +
				(string)(tcnt + 1),
				procedure(self)
				{
					if self.state
						print(self.caption, " down!\n");
					else
						print(self.caption, " up!\n");
				});
		tcnt += 1;
	  }
	  case 2
		container = gui.Panel(container);
	  case 3
		container = gui.Panel(container, "flat");
	  case 4
	  {
		local c = gui.Panel(container);
		c.colrowratio = 0;
		c.rowratio = vector [6, 1];
		local p = gui.Plotter(c, -50, 50);
		c = gui.Panel(c, "none");
		c.colrowratio = 1000;
		c.colratio = vector [6, 1, 1];
		local hs = gui.HSlider(c,
				procedure(self, v)
				{
					self.plotter:Input(v);
				},
				function(self, v)
				{
					return v * 100 - 50;
				},
				function(self, v)
				{
					return (v + 50) / 100;
				});
		hs.plotter = p;
		local b = gui.Button(c, "-",
				procedure(self)
				{
					self.plotter.min *= 1.25;
					self.plotter.max *= 1.25;
					self.plotter:Invalidate();
				});
		b.plotter = p;
		b = gui.Button(c, "+",
				procedure(self)
				{
					self.plotter.min /= 1.25;
					self.plotter.max /= 1.25;
					self.plotter:Invalidate();
				});
		b.plotter = p;
	  }
	  case 5
		gui.HSlider(container,
				procedure(self, v)
				{
					print("value = ",
						v, "\n");
				},
				function(self, v)
				{
					return v * 100;
				},
				function(self, v)
				{
					return v / 100;
				});
	  case 6
	  {
		local l = gui.Lissajous(container, 256);
		l.time = 0;
		ls.+ l;
	  }
	  case 7
		gui.LineEditor(container, "Edit");
	  case 8
	  {
		local tb = gui.TextBox(container);
		tb:Add("This is an gui TextBox.");
		tb:Add("It is intended for scrolling message log displays,");
		tb:Add("and similar applications, where editing is not needed.");
		for local i = 4, 40
			tb:Add("TextBox line #" + (string)i);
		tb:GoToTop();
	  }
	  case 9
	  {
		local g = gui.Graph(container);
		local dots = g:Curve("Dot Curve", "dot", 255, 64, 0);
		local squares = g:Curve("Square Curve", "square", 0, 255, 0);
		local boxes = g:Curve("Box Curve", "box", 0, 64, 255);
		local rings = g:Curve("Ring Curve", "ring", 200, 64, 200);
		local lines = g:Curve("Line Curve", "none", 200, 200, 200);
		for local i = 0, 1, .01
		{
			dots:Add(i, .5 * sin(i * 2 * PI));
			squares:Add(i, .5 * sin(i * 2 * PI + (PI / 4)));
			boxes:Add(i, .5 * sin(i * 2 * PI + (PI / 2)));
			rings:Add(i, .5 * sin(i * 2 * PI + (PI * 3 / 4)));
			lines:Add(i, .25 * sin(i * 6 * PI));
		}
		dots:AutoScale(true, true);
		squares:AutoScale(true, true);
		boxes:AutoScale(true, true);
		rings:AutoScale(true, true);
		lines:AutoScale(true, true);
	  }
	  case 10
		gui.VSlider(container,
				procedure(self, v)
				{
					print("value = ",
						v, "\n");
				},
				function(self, v)
				{
					return v * 100;
				},
				function(self, v)
				{
					return v / 100;
				});
	gui.Invalidate();
}


export function main<args>
{
	local window = SDL.Window ["Eelium GUI Test",
			SDL.WINDOWPOS_CENTERED, SDL.WINDOWPOS_CENTERED,
			640, 480, SDL.WINDOW_RESIZABLE];
	local renderer = SDL.Renderer [window];
	gui.Open(window, renderer);

	local g = gui.Root();
	g.colrowratio = 0;
	g.rowratio = vector [2, 20];
	local sb = gui.Panel(g)
		:SetAlign("grid", 7, 2);
	sb.colrowratio = 1000;
	gui.Button(sb, "CLEAR", procedure(self)
			{
				delete(ls);
				work:DeleteWidgets();
				container = work;
			});
	gui.Button(sb, "Button", procedure(self) { spawn(0); } );
	gui.Button(sb, "Toggle", procedure(self) { spawn(1); } );
	gui.Button(sb, "Panel", procedure(self) { spawn(2); } );
	gui.Button(sb, "FlatPanel", procedure(self) { spawn(3); } );
	gui.Button(sb, "Plotter", procedure(self) { spawn(4); } );
	gui.Button(sb, "EXIT", procedure(self) { ask_exit(); });
	
	gui.Button(sb, "HSlider", procedure(self) { spawn(5); } );
	gui.Button(sb, "VSlider", procedure(self) { spawn(10); } );
	gui.Button(sb, "Lissajous", procedure(self) { spawn(6); } );
	gui.Button(sb, "LineEditor", procedure(self) { spawn(7); } );
	gui.Button(sb, "TextBox", procedure(self) { spawn(8); } );
	gui.Button(sb, "Graph", procedure(self) { spawn(9); } );
	work, container = gui.Panel(g, "flat");
	local last_tick = SDL.GetTicks();
	while not do_exit
	{
		while true
		{
			local ev = SDL.PollEvent();
			if not ev
				break;
			switch ev.type
			  case SDL.KEYDOWN
			  {
				switch ev.sym
				  case SDL.KF1, SDL.KF2, SDL.KF3, SDL.KF4,
						SDL.KF5, SDL.KF6, SDL.KF7,
						SDL.KF8, SDL.KF9, SDL.KF10,
						SDL.KF11
					spawn(ev.sym - SDL.KF1);
				  case SDL.KF12
				  	spawn(last_tick);
				  default
					gui.Dispatch(ev);
			  }
			  case SDL.WINDOWEVENT
			  {
				switch ev.event
				  case SDL.WINDOWEVENT_RESIZED
					g:SetRect(SDL.Rect [0, 0, ev.(w, h)]);
				gui.Dispatch(ev);
			  }
			  case SDL.QUIT
				ask_exit();
			  default
				gui.Dispatch(ev);
		}

		local tick = SDL.GetTicks();
		local dt = tick - last_tick;
		last_tick = tick;
		if sizeof ls and dt and not askexit_up
			for local i = 0, sizeof ls - 1
			{
				local t = ls[i].time;
				for local s = 0, dt - 1
				{
				
					ls[i]:Input(sin(t), cos(t * 1.1));
					t += .02;
				}
				ls[i].time = t;
			}

		local dr = [];
		gui.Refresh(dr);
		if sizeof dr
			SDL.RenderPresent(renderer);
		SDL.Delay(10);
	}

	gui.Close();
	return 0;
}
