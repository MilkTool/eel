///////////////////////////////////////////////////////
// guiscroll.eel - Eelium GUI scroll test
// Copyright 2006-2007, 2009, 2011, 2014 David Olofson
///////////////////////////////////////////////////////

import SDL as SDL;
import eelium.text as text;
import eelium.gui as gui;
import math;


export function main<args>
{
	local do_exit = false;
	local bpp = 0;
	local flags = SDL.SWSURFACE | SDL.RESIZABLE;
	local screen = SDL.SetVideoMode(640, 480, bpp, flags);
	SDL.EnableKeyRepeat(250, 30);
	SDL.SetCaption("Eelium GUI Scroll Test", args[0]);

	gui.Open(screen);
	local g = gui.Root();
	g:SetAlign("grid", 2, 2, [62, 2], [46, 2]);

	local sb, g.scrollbox = gui.Panel(g, "depression");
	sb.border = 10;
	sb:SetAlign("relative", 0, 0);
	sb.yslider = gui.VSlider(g,
			procedure (self, v)
			{
				self.parent.scrollbox:SetScrollPos(nil, v);
			});
	sb.xslider = gui.HSlider(g,
			procedure (self, v)
			{
				self.parent.scrollbox:SetScrollPos(v, nil);
			});

// TODO: Somehow hook into the aligment system and reset
// TODO: scroll positions and gray out sliders as needed.
// TODO: Implement optional standard sliders in Panel?

	for local i = 0, 255
	{
		local lb = gui.Button(g.scrollbox, (string)i);
		local xx = i % 16;
		local yy = (integer)(i / 16);
		lb.logicrect.(x, y, w, h) = xx * 192, yy * 128, 128, 64;
	}
	
	while not do_exit
	{
		local ev = SDL.PollEvent();
		if not ev
		{
			local dr = array [];
			gui.Refresh(dr);
			if sizeof dr
				SDL.Update(dr);
			ev = SDL.WaitEvent();
		}
		switch ev.type
		  case SDL.KEYDOWN
		  {
			switch ev.sym
			  case SDL.KESCAPE
				do_exit = true;
			  default
				gui.Dispatch(ev);
		  }
		  case SDL.VIDEOEXPOSE
			gui.Invalidate();
		  case SDL.VIDEORESIZE
		  {
			screen = SDL.SetVideoMode(ev.w, ev.h, bpp, flags);
			g:SetRect(SDL.Rect [0, 0, ev.(w, h)]);
			gui.Invalidate();
		  }
		  case SDL.QUIT
			do_exit = true;
		  default
			gui.Dispatch(ev);
	}
	gui.Close();
	return 0;
}
