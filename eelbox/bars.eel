////////////////////////////////////////////////
// bars.eel - EELBox "Copper Bars"
// Copyright 2005, 2007, 2010 David Olofson
////////////////////////////////////////////////
// This example also demonstrates how to put
// an imported module (SDL, in this case) in
// it's own namespace. This can be handy if
// there are conflicts between modules, or to
// have the code explicitly show where things
// belong.
//    Note that the import namespace applies
// to *everything* imported from the module;
// not just functions.
////////////////////////////////////////////////

import "SDL" as SDL, "math";

function new_gradient(height)
{
	local gr = [];
	for local y = 0, height - 1
		gr[y] = SDL.MapColor(nil, 0, 0, 0);
	return gr;
}

procedure clear(gr)
{
	for local y = 0, sizeof gr - 1
		gr[y] = SDL.MapColor(nil, 0, 0, 0);
}

function bar(r, g, b, h)
{
	local gr = new_gradient(h);
	for local y = 0, h - 1
	{
		local s = 55 + sin(sin(PI * y / h)) * 200;
		gr[y] = SDL.MapColor(nil, r * s, g * s, b * s);
	}
	return gr;
}

procedure draw(src, dst, y)
{
	for local yy = 0, sizeof src - 1
		dst[yy + y] = src[yy];
}

procedure render(screen, gr)
{
	local r = SDL.Rect[0, 0, screen.w, 1];
	for local y = 0, sizeof gr - 1
	{
		r.y = y;
		SDL.FillRect(screen, r, gr[y]);
	}
}

export function main<args>
{
	local screen = SDL.SetVideoMode(320, 240, 0, 0);
	SDL.SetCaption("EELBox Copper Bars", args[0]);
	SDL.ShowCursor(0);

	local buffer = new_gradient(screen.h);

	local barh = 32;
	local bars = [];
	for local i = 0, 20
		bars[i] = bar((i % 8) / 7,
				((i * 2) % 9) / 8,
				((i * 3) % 10) / 9, barh);
	local a = (screen.h - barh) / 2;
	local sky = bar(0, .2, .5, screen.h);

	local start = SDL.GetTicks();
	while true
	mainloop:
	{
		local t = SDL.GetTicks() - start;
		while true
		{
			local ev = SDL.PollEvent();
			if not ev
				break;
			switch ev.type
			  case SDL.KEYUP
				if ev.sym == SDL.KESCAPE
					break mainloop;
			  case SDL.MOUSEBUTTONUP, SDL.QUIT
				break mainloop;
		}
		draw(sky, buffer, 0);
		for local i = 0, sizeof bars - 1
			draw(bars[i], buffer,
					a + sin(t / 500 + i / 10) * a);
		render(screen, buffer);
		SDL.Flip();
		SDL.Delay(0);	// In case we don't have retrace sync...
	}
	return 0;
}
