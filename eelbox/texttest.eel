/////////////////////////////////////////////
// texttest.eel - Test of text.eel
// Copyright (C) 2005 David Olofson
/////////////////////////////////////////////

import "SDL", "math";
import "text" as text;

export function main<args>
{
	local screen = SetVideoMode(640, 480, 0, SWSURFACE);
	SetCaption("EELBox Text Test", args[0]);
	ShowCursor(0);

	local f = text.Open("microfont_sh.png");
	local xc, local yc = (screen.w, screen.h) / 2;
	local xa, local ya = (xc, yc) - (130, 10) / 2;
	local last_tick = GetTicks();
	local t = 0;
	local slow = true;
	while true
	mainloop:
	{
		local tick = GetTicks();
		local dt = tick - last_tick;
		last_tick = tick;
		if slow
			t += dt * .2;
		else
			t += dt;

		while true
		{
			local ev = PollEvent();
			if not ev
				break;
			switch ev.type
			  case KEYDOWN
				if ev.sym == KSPACE
					slow = false;
			  case KEYUP
				if ev.sym == KESCAPE
					break mainloop;
				else if ev.sym == KSPACE
					slow = true;
			  case MOUSEBUTTONUP, QUIT
				break mainloop;
		}

		FillRect(nil, nil, MapColor(nil, 20, 40, 30));
		local stx, local sty = t / (1000, 1400);
		for local i = 0, 100
		{
			stx, sty += .07, .09;
			local x = (integer)(xc + cos(stx) * xa - 65);
			local y = (integer)(yc + sin(sty) * ya - 5);
			f:PutString("Hello, world! (" + (string)x +
					"," + (string)y + ")", x, y);
		}

		Flip();
		Delay(0);
	}
	return 0;
}
