////////////////////////////////////////////////////////////////
// drawtools.eel - SDL draw tools for the EElium GUI and themes
// Copyright 2005, 2010-2011, 2014, 2019 David Olofson
////////////////////////////////////////////////////////////////

module eelium.drawtools;

import SDL as SDL;


export procedure box(target, rect, color)
{
	local r = clone rect;
	SDL.SetRenderDrawColor(target, color);

	// Top and bottom
	r.h = 1;
	SDL.RenderFillRect(target, clone r);
	r.y += rect.h - 1;
	SDL.RenderFillRect(target, clone r);

	// Left and right
	r.(y, w, h) = rect.y + 1, 1, rect.h - 2;
	SDL.RenderFillRect(target, clone r);
	r.x += rect.w - 1;
	SDL.RenderFillRect(target, r);
}


export procedure box3d(target, rect, tlc, cc, brc)
{
	local r = clone rect;
	// Top and bottom
	r.h = 1;
	r.w -= 1;
	SDL.SetRenderDrawColor(target, tlc);
	SDL.RenderFillRect(target, clone r);
	r.x += 1;
	r.y += rect.h - 1;
	SDL.SetRenderDrawColor(target, brc);
	SDL.RenderFillRect(target, clone r);

	// Left and right
	r.(x, y, w, h) = rect.(x, y), 1, rect.h - 1;
	SDL.SetRenderDrawColor(target, tlc);
	SDL.RenderFillRect(target, clone r);
	r.x += rect.w - 1;
	r.y += 1;
	SDL.SetRenderDrawColor(target, brc);
	SDL.RenderFillRect(target, clone r);

	// Highlight/shadow crossover corners
	SDL.SetRenderDrawColor(target, cc);
	r.(x, y) = rect.x + rect.w - 1, rect.y;
	r.(w, h) = 1;
	SDL.RenderFillRect(target, clone r);
	r.(x, y) = rect.x, rect.y + rect.h - 1;
	SDL.RenderFillRect(target, r);
}


export procedure rbox3d(target, rect, tlc, cc, brc, hlc, shc)
{
	local r = clone rect;
	// Top and bottom
	r.h = 1;
	r.w -= 1;
	SDL.SetRenderDrawColor(target, tlc);
	SDL.RenderFillRect(target, clone r);
	r.x += 1;
	r.y += rect.h - 1;
	SDL.SetRenderDrawColor(target, brc);
	SDL.RenderFillRect(target, clone r);

	// Left and right
	r.(x, y, w, h) = rect.(x, y), 1, rect.h - 1;
	SDL.SetRenderDrawColor(target, tlc);
	SDL.RenderFillRect(target, clone r);
	r.x += rect.w - 1;
	r.y += 1;
	SDL.SetRenderDrawColor(target, brc);
	SDL.RenderFillRect(target, clone r);

	// Highlight/shadow crossover corners
	SDL.SetRenderDrawColor(target, cc);
	r.(x, y) = rect.x + rect.w - 1, rect.y;
	r.(w, h) = 1;
	SDL.RenderFillRect(target, clone r);
	r.(x, y) = rect.x, rect.y + rect.h - 1;
	SDL.RenderFillRect(target, clone r);

	// Highlight corner
	r.(x, y) = rect.x, rect.y;
	SDL.SetRenderDrawColor(target, hlc);
	SDL.RenderFillRect(target, clone r);

	// Shadow corner
	r.(x, y) = rect.x + rect.w - 1, rect.y + rect.h - 1;
	SDL.SetRenderDrawColor(target, shc);
	SDL.RenderFillRect(target, r);
}
